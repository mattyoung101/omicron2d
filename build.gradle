plugins {
    id 'java'
    id 'application'
    id 'org.openjfx.javafxplugin' version '0.0.8'
    id 'org.jetbrains.kotlin.jvm' version '1.5.20'
    id "org.graalvm.plugin.compiler" version "0.1.0-alpha2"
}

// Kotlin versions: https://kotlinlang.org/releases.html

group 'omicron'
version '1.0-SNAPSHOT'

apply plugin: 'antlr'

sourceCompatibility = 1.8

// fix to stop gradle from nuking my source code due to some random bug
// NOTE: inherited from our last Kotlin project, Omicontrol, may be fixed by now? please?
apply plugin: 'idea'
idea {
    module {
        downloadJavadoc = true
        downloadSources = true
    }
}

repositories {
    //mavenLocal()
    mavenCentral()
    google()
    maven { url "https://oss.sonatype.org/content/repositories/snapshots/" }
    maven { url "https://oss.sonatype.org/content/repositories/releases/" }
    // we had to remove jcenter, but if it blows up, we might have to add that back
}

dependencies {
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8"
    implementation group: 'org.jetbrains.kotlin', name: 'kotlin-reflect', version: '1.5.20'

    implementation 'org.tinylog:tinylog-api-kotlin:2.0.0'
    implementation 'org.tinylog:tinylog-impl:2.0.0'
    implementation group: 'org.apache.commons', name: 'commons-math3', version: '3.6.1' // note: watch for math4 release
    implementation group: 'org.apache.commons', name: 'commons-lang3', version: '3.9'
    implementation group: 'org.apache.commons', name: 'commons-collections4', version: '4.4'
    implementation group: 'commons-io', name: 'commons-io', version: '2.6'
    implementation 'com.google.code.gson:gson:2.8.6'
    implementation group: 'com.esotericsoftware.yamlbeans', name: 'yamlbeans', version: '1.14'
    implementation group: 'net.objecthunter', name: 'exp4j', version: '0.4.8'
    implementation group: 'org.locationtech.jts', name: 'jts-core', version: '1.18.0'
    implementation group: 'org.jfree', name: 'jfreechart', version: '1.5.0' // TODO REMOVE
    implementation group: 'org.jfree', name: 'jfreechart-fx', version: '1.0.1' // TODO REMOVE
    implementation 'com.github.fzakaria:ascii85:1.2' // TODO possibly remove
    // our lovely friends at Oracle (/s) have deprecated Nashorn for some super annoying reason, so we drag in GraalJS
    // as a replacement, for evaluating class instantiations in behaviour trees
    implementation "org.graalvm.sdk:graal-sdk:20.2.0"
    implementation "org.graalvm.js:js:20.2.0"
    implementation "org.graalvm.js:js-scriptengine:20.2.0"
    implementation "org.reflections:reflections:0.9.12"

    antlr "org.antlr:antlr4:4.9"
    implementation "org.antlr:antlr4-runtime:4.9"

    testImplementation group: 'junit', name: 'junit', version: '4.12'
}

test {
    useJUnit()
}

javafx {
    version = "11"
    modules = [ 'javafx.controls', 'javafx.fxml' ]
}

mainClassName = "io.github.omicron2d.Main"

compileKotlin {
    kotlinOptions.jvmTarget = "1.8"
    // use new Kotlin IR backend
    // ref: https://blog.jetbrains.com/kotlin/2021/02/the-jvm-backend-is-in-beta-let-s-make-it-stable-together/
    kotlinOptions.useIR = true
}
compileTestKotlin {
    kotlinOptions.jvmTarget = "1.8"
    kotlinOptions.useIR = true
}